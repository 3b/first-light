(in-package #:virality)

(defun load-initial-scene (core scene-name)
  (let ((scene-name (or scene-name =initial-scene=)))
    (make-prefab-instance core scene-name)))

(defun initialize (core project scene-name)
  (load-config project)
  (setup-repl)
  (initialize-rng)
  (prepare-gamepads)
  (make-display core)
  (make-input-data core)
  (load-hardware-info)
  (make-thread-pool)
  (load-graphs core)
  (load-call-flows core)
  (initialize-shaders core)
  (make-clock core)
  (tex::load-texture-descriptors core)
  (mat::load-materials core)
  (col::initialize-collider-system core)
  (make-scene-tree core)
  (load-initial-scene core scene-name)
  (run-prologue core)
  (start-loop core))

(defun deinitialize (core)
  (run-epilogue core)
  (kill-display core)
  (shutdown-gamepads core)
  (sdl2:quit)
  (destroy-thread-pool)
  (makunbound '*core-debug*))

(defun start-loop (core)
  (let ((context (context core))
        (input-data (input-data core)))
    (initialize-frame-time (clock core))
    (u:while (running-p core)
      (with-continuable "Virality Engine"
        (handle-events input-data)
        (render-frame core)
        ;; TODO: Remove this later when possible.
        (when (on-button-enter context :key :escape)
          (stop core))))))

(defun start (&key project scene)
  (let ((core (make-core project)))
    (unwind-protect (initialize core project scene)
      (deinitialize core))))

(defun stop (core)
  (setf (running-p core) nil))
